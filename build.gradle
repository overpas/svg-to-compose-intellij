plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.13.3'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'io.gitlab.arturbosch.detekt' version '1.23.0'
}

group 'by.overpass'
version '0.11'

repositories {
    mavenCentral()
    maven { url "https://maven.google.com" }
    maven { url "https://jitpack.io" }
}

ext {
    junitVersion = "4.13.2"
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.1'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.22"
    implementation('com.github.DevSrSouza:svg-to-compose:0.8.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:5.0.0"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version.set("221.6008.13")
    plugins.set(['java', 'Kotlin'])
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of("11"))
    }
}

signPlugin {
    certificateChain.set(System.getenv("CERTIFICATE_CHAIN"))
    privateKey.set(System.getenv("PRIVATE_KEY"))
    password.set(System.getenv("PRIVATE_KEY_PASSWORD"))
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"))
}

tasks {
    patchPluginXml {
        sinceBuild.set("221.6008.13")
        untilBuild.set("232.9559.34")
    }
}

patchPluginXml {
    changeNotes.set("""
        Handle copy-pasted paths in output directory and vector images directory
      """)
}

test {
    useJUnit()
}