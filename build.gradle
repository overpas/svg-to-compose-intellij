plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '1.9.0'
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
    id 'io.gitlab.arturbosch.detekt' version '1.21.0'
}

group 'by.overpass'
version '0.5'

repositories {
    mavenCentral()
    maven { url "https://maven.google.com" }
    maven { url "https://jitpack.io" }
}

ext {
    junitVersion = "4.13.2"
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.7.20'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.20"
    implementation('com.github.DevSrSouza:svg-to-compose:0.8.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.0.0"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version.set("213.7172.25")
    plugins.set(['java', 'Kotlin'])
}

signPlugin {
    certificateChain.set(System.getenv("CERTIFICATE_CHAIN"))
    privateKey.set(System.getenv("PRIVATE_KEY"))
    password.set(System.getenv("PRIVATE_KEY_PASSWORD"))
}

publishPlugin {
    token.set(System.getenv("PUBLISH_TOKEN"))
}

patchPluginXml {
    changeNotes.set("""
      A crash on Android Studio Dolphin fixed;
      UI tweaks.
      """)
}

test {
    useJUnit()
}